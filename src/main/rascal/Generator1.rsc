module Generator1

import AST;
import Syntax;
import Parser;
import Implode;
import IO;
import List;
import String;

data GenTask = genTask(str action = "", int prio = 0, str duration = "");

list[str] allPersons = [];
list[GenTask] allPlans = [];

void generate(Planning planning) {
    for (personTask <- planning.personList) {
        generate(personTask);
    }
}

void generate(PersonTasks personTasks) {
    allPersons += "<personTasks.name>";
    for (task <- personTasks.tasks) {
        generate(task);
    }
}

void generate(Task task) {
    rVal = genTask(action = generateAction(task.action));
    rVal.prio = task.prio;
    for (dur <- task.duration) {
        rVal.duration = generateDuration(dur);
    }
    allPlans += rVal;
}

str generateAction(Action action) {
    if (action.lunchAction?) return generateAction(action.lunchAction);
    if (action.meetingAction?) return generateAction(action.meetingAction);
    if (action.paperAction?) return generateAction(action.paperAction);
    if (action.paymentAction?) return generateAction(action.paymentAction);
    return "Unknown action!";
}

str generateAction(LunchAction lunchAction) {
    return "Lunch at location <lunchAction.location>";
}

str generateAction(MeetingAction meetingAction) {
    return "Meeting about <meetingAction.topic>";
}

str generateAction(PaperAction paperAction) {
    return "Report <paperAction.report>";
}

str generateAction(PaymentAction paymentAction) {
    return "Pay <paymentAction.amount> Euro";
}

str generateDuration(Duration dur) {
    return "with duration: <dur.dl> <generateDuration(dur.unit)>";
}

str generateDuration(TimeUnit timeUnit) {
    if (timeUnit.minute?) return "m";
    if (timeUnit.hour?) return "h";
    if (timeUnit.day?) return "d";
    if (timeUnit.week?) return "w";
    return "";
}

str generator1(Planning ast) {
    allPersons = [];
    allPlans = [];
    generate(ast);
    
    rVal = "Info of the planning DepartmentABC
           'All Persons:
           '<for (person <- allPersons) {><person>
           '<}>
           'All actions of tasks:
           '======
           '<intercalate(" &\n", 
               ["<plan.action> <plan.prio> <plan.duration>" | plan <- allPlans])>
           '=====
           'Other way of listing all tasks:
           '<intercalate(" ,\n", 
               ["<plan.action> <plan.prio>" | plan <- allPlans])>
           '";
    
    return rVal;
}

void main() {
    cast = parsePlanning(|project://rascaldsl/instance/spec1.tdsl|);
    ast = implode(cast);
    result = generator1(ast);
    println(result);
    writeFile(|project://rascaldsl/instance/output/generator1.txt|, result);
}